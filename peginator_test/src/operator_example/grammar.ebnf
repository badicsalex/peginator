# Copyright (C) 2022, Alex Badics
# This file is part of peginator
# Licensed under the MIT license. See LICENSE file in the project root for details.

@export
Assignment = lvalue:Identifier '=' expr:Expression ';'$;

Expression = @:Additive | @:Term;
Additive = parts:Term {(parts:Plus | parts:Minus) parts:Term}+;
Plus = '+';
Minus = '-';

Term = @:Multiplicative | @:Factor;
Multiplicative = parts:Factor {(parts:Mul | parts:Div) parts:Factor}+;
Mul = '*';
Div = '/';

Factor = @:Group | @:Number;
Group = '(' body:*Expression ')';

@string
@no_skip_ws
Number = {'0'..'9'}+;

@string
@no_skip_ws
Identifier = {'a'..'z' | '0'..'9' | '_'}+;
